# Overview

Excellent gets its name from borrowing some of the syntax and function names of formulas in Microsoft Excelâ„¢, 
though it has evolved over time and similarities are now much fewer. It is an expression based templating 
language which aims to make it easy to generate text from a context of values.

# Templates

Templates can contain single variables or more complex expressions. A single variable is embedded using the `@` 
character. For example the template `Hi @foo` contains a single variable which at runtime will be replaced with 
with the value of `foo` in the context.

More complex expressions can be embedded using the `@(...)` syntax. For example the template `Hi @("Dr " & upper(foo))` 
takes the value of `foo`, converts it to uppercase, and the prefixes it with another string. Note than within a 
complex expression you don't prefix variables with `@`.

The `@` symbol can be escaped in templates by repeating it, ie, `Hi @@twitter` would output `Hi @twitter`.

# Types

Excellent has the following types:

 * [Array](#type:array)
 * [Boolean](#type:boolean)
 * [Date](#type:date)
 * [DateTime](#type:datetime)
 * [Function](#type:function)
 * [Number](#type:number)
 * [Object](#type:object)
 * [Text](#type:text)
 * [Time](#type:time)

<div class="types">
<a name="type:array"></a>

## Array

Is an array of items.


```objectivec
@(array(1, "x", true)) â†’ [1, x, true]
@(array(1, "x", true)[1]) â†’ x
@(count(array(1, "x", true))) â†’ 3
@(json(array(1, "x", true))) â†’ [1,"x",true]
```

<a name="type:boolean"></a>

## Boolean

Is a boolean `true` or `false`.


```objectivec
@(true) â†’ true
@(1 = 1) â†’ true
@(1 = 2) â†’ false
@(json(true)) â†’ true
```

<a name="type:date"></a>

## Date

Is a Gregorian calendar date value.


```objectivec
@(date_from_parts(2019, 4, 11)) â†’ 2019-04-11
@(format_date(date_from_parts(2019, 4, 11))) â†’ 11-04-2019
@(json(date_from_parts(2019, 4, 11))) â†’ "2019-04-11"
```

<a name="type:datetime"></a>

## Datetime

Is a datetime value.


```objectivec
@(datetime("1979-07-18T10:30:45.123456Z")) â†’ 1979-07-18T10:30:45.123456Z
@(format_datetime(datetime("1979-07-18T10:30:45.123456Z"))) â†’ 18-07-1979 05:30
@(json(datetime("1979-07-18T10:30:45.123456Z"))) â†’ "1979-07-18T10:30:45.123456Z"
```

<a name="type:function"></a>

## Function

Is a callable function.


```objectivec
@(upper) â†’ function
@(array(upper)[0]("abc")) â†’ ABC
@(json(upper)) â†’ "function"
```

<a name="type:number"></a>

## Number

Is a whole or fractional number.


```objectivec
@(1234) â†’ 1234
@(1234.5678) â†’ 1234.5678
@(format_number(1234.5678)) â†’ 1,234.57
@(json(1234.5678)) â†’ 1234.5678
```

<a name="type:object"></a>

## Object

Is an object with named properties.


```objectivec
@(object("foo", 1, "bar", "x")) â†’ {bar: x, foo: 1}
@(object("foo", 1, "bar", "x").bar) â†’ x
@(object("foo", 1, "bar", "x")["bar"]) â†’ x
@(count(object("foo", 1, "bar", "x"))) â†’ 2
@(json(object("foo", 1, "bar", "x"))) â†’ {"bar":"x","foo":1}
```

<a name="type:text"></a>

## Text

Is a string of characters.


```objectivec
@("abc") â†’ abc
@(text_length("abc")) â†’ 3
@(upper("abc")) â†’ ABC
@(json("abc")) â†’ "abc"
```

<a name="type:time"></a>

## Time

Is a time of day.


```objectivec
@(time_from_parts(16, 30, 45)) â†’ 16:30:45.000000
@(format_time(time_from_parts(16, 30, 45))) â†’ 16:30
@(json(time_from_parts(16, 30, 45))) â†’ "16:30:45.000000"
```


</div>

# Operators

<div class="operators">
<a name="operator:add"></a>

## Add

Adds two numbers.


```objectivec
@(2 + 3) â†’ 5
@(fields.age + 10) â†’ 33
```

<a name="operator:concatenate"></a>

## Concatenate

Joins two text values together.


```objectivec
@("hello" & " " & "bar") â†’ hello bar
@("hello" & null) â†’ hello
```

<a name="operator:divide"></a>

## Divide

Divides a number by another.


```objectivec
@(4 / 2) â†’ 2
@(3 / 2) â†’ 1.5
@(46 / fields.age) â†’ 2
@(3 / 0) â†’ ERROR
```

<a name="operator:equal"></a>

## Equal

Returns true if two values are textually equal.


```objectivec
@("hello" = "hello") â†’ true
@("hello" = "bar") â†’ false
@(1 = 1) â†’ true
```

<a name="operator:exponent"></a>

## Exponent

Raises a number to the power of a another number.


```objectivec
@(2 ^ 8) â†’ 256
```

<a name="operator:greaterthan"></a>

## Greaterthan

Returns true if the first number is greater than the second.


```objectivec
@(2 > 3) â†’ false
@(3 > 3) â†’ false
@(4 > 3) â†’ true
```

<a name="operator:greaterthanorequal"></a>

## Greaterthanorequal

Returns true if the first number is greater than or equal to the second.


```objectivec
@(2 >= 3) â†’ false
@(3 >= 3) â†’ true
@(4 >= 3) â†’ true
```

<a name="operator:lessthan"></a>

## Lessthan

Returns true if the first number is less than the second.


```objectivec
@(2 < 3) â†’ true
@(3 < 3) â†’ false
@(4 < 3) â†’ false
```

<a name="operator:lessthanorequal"></a>

## Lessthanorequal

Returns true if the first number is less than or equal to the second.


```objectivec
@(2 <= 3) â†’ true
@(3 <= 3) â†’ true
@(4 <= 3) â†’ false
```

<a name="operator:multiply"></a>

## Multiply

Multiplies two numbers.


```objectivec
@(3 * 2) â†’ 6
@(fields.age * 3) â†’ 69
```

<a name="operator:negate"></a>

## Negate

Negates a number


```objectivec
@(-fields.age) â†’ -23
```

<a name="operator:notequal"></a>

## Notequal

Returns true if two values are textually not equal.


```objectivec
@("hello" != "hello") â†’ false
@("hello" != "bar") â†’ true
@(1 != 2) â†’ true
```

<a name="operator:subtract"></a>

## Subtract

Subtracts two numbers.


```objectivec
@(3 - 2) â†’ 1
@(2 - 3) â†’ -1
```


</div>

# Functions

Expressions have access to a set of built-in functions which can be used to perform more complex tasks. Functions are called 
using the `@(function_name(args..))` syntax, and can take as arguments either literal values `@(length(split("1 2 3", " "))` 
or variables in the context `@(title(contact.name))`.

<div class="functions">
<a name="function:abs"></a>

## abs(num)

Returns the absolute value of `num`.


```objectivec
@(abs(-10)) â†’ 10
@(abs(10.5)) â†’ 10.5
@(abs("foo")) â†’ ERROR
```

<a name="function:and"></a>

## and(values...)

Returns whether all the given `values` are truthy.


```objectivec
@(and(true)) â†’ true
@(and(true, false, true)) â†’ false
```

<a name="function:array"></a>

## array(values...)

Takes multiple `values` and returns them as an array.


```objectivec
@(array("a", "b", 356)[1]) â†’ b
@(join(array("a", "b", "c"), "|")) â†’ a|b|c
@(count(array())) â†’ 0
@(count(array("a", "b"))) â†’ 2
```

<a name="function:attachment_parts"></a>

## attachment_parts(attachment)

Parses an attachment into its different parts


```objectivec
@(attachment_parts("image/jpeg:https://example.com/test.jpg")) â†’ {content_type: image/jpeg, url: https://example.com/test.jpg}
```

<a name="function:boolean"></a>

## boolean(value)

Tries to convert `value` to a boolean.

An error is returned if the value can't be converted.


```objectivec
@(boolean(array(1, 2))) â†’ true
@(boolean("FALSE")) â†’ false
@(boolean(1 / 0)) â†’ ERROR
```

<a name="function:char"></a>

## char(code)

Returns the character for the given UNICODE `code`.

It is the inverse of [code](expressions.html#function:code).


```objectivec
@(char(33)) â†’ !
@(char(128512)) â†’ ğŸ˜€
@(char("foo")) â†’ ERROR
```

<a name="function:clean"></a>

## clean(text)

Strips any non-printable characters from `text`.


```objectivec
@(clean("ğŸ˜ƒ Hello \nwo\tr\rld")) â†’ ğŸ˜ƒ Hello world
@(clean(123)) â†’ 123
```

<a name="function:code"></a>

## code(text)

Returns the UNICODE code for the first character of `text`.

It is the inverse of [char](expressions.html#function:char).


```objectivec
@(code("a")) â†’ 97
@(code("abc")) â†’ 97
@(code("ğŸ˜€")) â†’ 128512
@(code("15")) â†’ 49
@(code(15)) â†’ 49
@(code("")) â†’ ERROR
```

<a name="function:count"></a>

## count(value)

Returns the number of items in the given array or properties on an object.

It will return an error if it is passed an item which isn't countable.


```objectivec
@(count(contact.fields)) â†’ 5
@(count(array())) â†’ 0
@(count(array("a", "b", "c"))) â†’ 3
@(count(1234)) â†’ ERROR
```

<a name="function:date"></a>

## date(value)

Tries to convert `value` to a date.

If it is text then it will be parsed into a date using the default date format.
An error is returned if the value can't be converted.


```objectivec
@(date("1979-07-18")) â†’ 1979-07-18
@(date("1979-07-18T10:30:45.123456Z")) â†’ 1979-07-18
@(date("10/05/2010")) â†’ 2010-05-10
@(date("NOT DATE")) â†’ ERROR
```

<a name="function:date_from_parts"></a>

## date_from_parts(year, month, day)

Creates a date from `year`, `month` and `day`.


```objectivec
@(date_from_parts(2017, 1, 15)) â†’ 2017-01-15
@(date_from_parts(2017, 2, 31)) â†’ 2017-03-03
@(date_from_parts(2017, 13, 15)) â†’ ERROR
```

<a name="function:datetime"></a>

## datetime(value)

Tries to convert `value` to a datetime.

If it is text then it will be parsed into a datetime using the default date
and time formats. An error is returned if the value can't be converted.


```objectivec
@(datetime("1979-07-18")) â†’ 1979-07-18T00:00:00.000000-05:00
@(datetime("1979-07-18T10:30:45.123456Z")) â†’ 1979-07-18T10:30:45.123456Z
@(datetime("10/05/2010")) â†’ 2010-05-10T00:00:00.000000-05:00
@(datetime("NOT DATE")) â†’ ERROR
```

<a name="function:datetime_add"></a>

## datetime_add(date, offset, unit)

Calculates the date value arrived at by adding `offset` number of `unit` to the `date`

Valid durations are "Y" for years, "M" for months, "W" for weeks, "D" for days, "h" for hour,
"m" for minutes, "s" for seconds


```objectivec
@(datetime_add("2017-01-15", 5, "D")) â†’ 2017-01-20T00:00:00.000000-05:00
@(datetime_add("2017-01-15 10:45", 30, "m")) â†’ 2017-01-15T11:15:00.000000-05:00
```

<a name="function:datetime_diff"></a>

## datetime_diff(date1, date2, unit)

Returns the duration between `date1` and `date2` in the `unit` specified.

Valid durations are "Y" for years, "M" for months, "W" for weeks, "D" for days, "h" for hour,
"m" for minutes, "s" for seconds.


```objectivec
@(datetime_diff("2017-01-15", "2017-01-17", "D")) â†’ 2
@(datetime_diff("2017-01-15", "2017-05-15", "W")) â†’ 17
@(datetime_diff("2017-01-15", "2017-05-15", "M")) â†’ 4
@(datetime_diff("2017-01-17 10:50", "2017-01-17 12:30", "h")) â†’ 1
@(datetime_diff("2017-01-17", "2015-12-17", "Y")) â†’ -2
```

<a name="function:datetime_from_epoch"></a>

## datetime_from_epoch(seconds)

Converts the UNIX epoch time `seconds` into a new date.


```objectivec
@(datetime_from_epoch(1497286619)) â†’ 2017-06-12T11:56:59.000000-05:00
@(datetime_from_epoch(1497286619.123456)) â†’ 2017-06-12T11:56:59.123456-05:00
```

<a name="function:default"></a>

## default(value, default)

Returns `value` if is not empty or an error, otherwise it returns `default`.


```objectivec
@(default(undeclared.var, "default_value")) â†’ default_value
@(default("10", "20")) â†’ 10
@(default("", "value")) â†’ value
@(default(array(1, 2), "value")) â†’ [1, 2]
@(default(array(), "value")) â†’ value
@(default(datetime("invalid-date"), "today")) â†’ today
@(default(format_urn("invalid-urn"), "ok")) â†’ ok
```

<a name="function:epoch"></a>

## epoch(date)

Converts `date` to a UNIX epoch time.

The returned number can contain fractional seconds.


```objectivec
@(epoch("2017-06-12T16:56:59.000000Z")) â†’ 1497286619
@(epoch("2017-06-12T18:56:59.000000+02:00")) â†’ 1497286619
@(epoch("2017-06-12T16:56:59.123456Z")) â†’ 1497286619.123456
@(round_down(epoch("2017-06-12T16:56:59.123456Z"))) â†’ 1497286619
```

<a name="function:extract"></a>

## extract(object, properties...)

Takes an object and extracts the named property.


```objectivec
@(extract(contact, "name")) â†’ Ryan Lewis
@(extract(contact.groups[0], "name")) â†’ Testers
```

<a name="function:extract_object"></a>

## extract_object(object, properties...)

Takes an object and returns a new object by extracting only the named properties.


```objectivec
@(extract_object(contact.groups[0], "name")) â†’ {name: Testers}
```

<a name="function:field"></a>

## field(text, index, delimiter)

Splits `text` using the given `delimiter` and returns the field at `index`.

The index starts at zero. When splitting with a space, the delimiter is considered to be all whitespace.


```objectivec
@(field("a,b,c", 1, ",")) â†’ b
@(field("a,,b,c", 1, ",")) â†’
@(field("a   b c", 1, " ")) â†’ b
@(field("a		b	c	d", 1, "	")) â†’
@(field("a\t\tb\tc\td", 1, " ")) â†’
@(field("a,b,c", "foo", ",")) â†’ ERROR
```

<a name="function:foreach"></a>

## foreach(array, func, [args...])

Takes an array of objects and returns a new array by applying the given function to each item.

If the given function takes more than one argument, you can pass additional arguments after the function.


```objectivec
@(foreach(array("a", "b", "c"), upper)) â†’ [A, B, C]
@(foreach(array("the man", "fox", "jumped up"), word, 0)) â†’ [the, fox, jumped]
```

<a name="function:format_date"></a>

## format_date(date, [,format])

Formats `date` as text according to the given `format`. If `format` is not
specified then the environment's default format is used.

The format string can consist of the following characters. The characters
' ', ':', ',', 'T', '-' and '_' are ignored. Any other character is an error.

* `YY`        - last two digits of year 0-99
* `YYYY`      - four digits of year 0000-9999
* `M`         - month 1-12
* `MM`        - month 01-12
* `D`         - day of month, 1-31
* `DD`        - day of month, zero padded 0-31


```objectivec
@(format_date("1979-07-18T15:00:00.000000Z")) â†’ 18-07-1979
@(format_date("1979-07-18T15:00:00.000000Z", "YYYY-MM-DD")) â†’ 1979-07-18
@(format_date("2010-05-10T19:50:00.000000Z", "YYYY M DD")) â†’ 2010 5 10
@(format_date("1979-07-18T15:00:00.000000Z", "YYYY")) â†’ 1979
@(format_date("1979-07-18T15:00:00.000000Z", "M")) â†’ 7
@(format_date("NOT DATE", "YYYY-MM-DD")) â†’ ERROR
```

<a name="function:format_datetime"></a>

## format_datetime(date [,format [,timezone]])

Formats `date` as text according to the given `format`. If `format` is not
specified then the environment's default format is used.

The format string can consist of the following characters. The characters
' ', ':', ',', 'T', '-' and '_' are ignored. Any other character is an error.

* `YY`        - last two digits of year 0-99
* `YYYY`      - four digits of year 0000-9999
* `M`         - month 1-12
* `MM`        - month 01-12
* `D`         - day of month, 1-31
* `DD`        - day of month, zero padded 0-31
* `h`         - hour of the day 1-12
* `hh`        - hour of the day 01-12
* `tt`        - twenty four hour of the day 01-23
* `m`         - minute 0-59
* `mm`        - minute 00-59
* `s`         - second 0-59
* `ss`        - second 00-59
* `fff`       - milliseconds
* `ffffff`    - microseconds
* `fffffffff` - nanoseconds
* `aa`        - am or pm
* `AA`        - AM or PM
* `Z`         - hour and minute offset from UTC, or Z for UTC
* `ZZZ`       - hour and minute offset from UTC

Timezone should be a location name as specified in the IANA Time Zone database, such
as "America/Guayaquil" or "America/Los_Angeles". If not specified, the current timezone
will be used. An error will be returned if the timezone is not recognized.


```objectivec
@(format_datetime("1979-07-18T15:00:00.000000Z")) â†’ 18-07-1979 10:00
@(format_datetime("1979-07-18T15:00:00.000000Z", "YYYY-MM-DD")) â†’ 1979-07-18
@(format_datetime("2010-05-10T19:50:00.000000Z", "YYYY M DD tt:mm")) â†’ 2010 5 10 14:50
@(format_datetime("2010-05-10T19:50:00.000000Z", "YYYY-MM-DD tt:mm AA", "America/Los_Angeles")) â†’ 2010-05-10 12:50 PM
@(format_datetime("1979-07-18T15:00:00.000000Z", "YYYY")) â†’ 1979
@(format_datetime("1979-07-18T15:00:00.000000Z", "M")) â†’ 7
@(format_datetime("NOT DATE", "YYYY-MM-DD")) â†’ ERROR
```

<a name="function:format_input"></a>

## format_input(urn)

Formats `input` to be the text followed by the URLs of any attachment, separated by newlines.


```objectivec
@(format_input(input)) â†’ Hi there\nhttp://s3.amazon.com/bucket/test.jpg\nhttp://s3.amazon.com/bucket/test.mp3
@(format_input("NOT INPUT")) â†’ ERROR
```

<a name="function:format_location"></a>

## format_location(location)

Formats the given `location` as its name.


```objectivec
@(format_location("Rwanda")) â†’ Rwanda
@(format_location("Rwanda > Kigali")) â†’ Kigali
```

<a name="function:format_number"></a>

## format_number(number, places [, humanize])

Formats `number` to the given number of decimal `places`.

An optional third argument `humanize` can be false to disable the use of thousand separators.


```objectivec
@(format_number(31337)) â†’ 31,337.00
@(format_number(31337, 2)) â†’ 31,337.00
@(format_number(31337, 2, true)) â†’ 31,337.00
@(format_number(31337, 0, false)) â†’ 31337
@(format_number("foo", 2, false)) â†’ ERROR
```

<a name="function:format_time"></a>

## format_time(time [,format])

Formats `time` as text according to the given `format`. If `format` is not
specified then the environment's default format is used.

The format string can consist of the following characters. The characters
' ', ':', ',', 'T', '-' and '_' are ignored. Any other character is an error.

* `h`         - hour of the day 1-12
* `hh`        - hour of the day 01-12
* `tt`        - twenty four hour of the day 01-23
* `m`         - minute 0-59
* `mm`        - minute 00-59
* `s`         - second 0-59
* `ss`        - second 00-59
* `fff`       - milliseconds
* `ffffff`    - microseconds
* `fffffffff` - nanoseconds
* `aa`        - am or pm
* `AA`        - AM or PM


```objectivec
@(format_time("14:50:30.000000")) â†’ 14:50
@(format_time("14:50:30.000000", "h:mm aa")) â†’ 2:50 pm
@(format_time("15:00:27.000000", "s")) â†’ 27
@(format_time("NOT TIME", "hh:mm")) â†’ ERROR
```

<a name="function:format_urn"></a>

## format_urn(urn)

Formats `urn` into human friendly text.


```objectivec
@(format_urn("tel:+250781234567")) â†’ 0781 234 567
@(format_urn("twitter:134252511151#billy_bob")) â†’ billy_bob
@(format_urn(contact.urn)) â†’ (206) 555-1212
@(format_urn(urns.tel)) â†’ (206) 555-1212
@(format_urn(urns.mailto)) â†’ foo@bar.com
@(format_urn("NOT URN")) â†’ ERROR
```

<a name="function:if"></a>

## if(test, value1, value2)

Returns `value1` if `test` is truthy or `value2` if not.

If the first argument is an error that error is returned.


```objectivec
@(if(1 = 1, "foo", "bar")) â†’ foo
@(if("foo" > "bar", "foo", "bar")) â†’ ERROR
```

<a name="function:is_error"></a>

## is_error(value)

Returns whether `value` is an error


```objectivec
@(is_error(datetime("foo"))) â†’ true
@(is_error(run.not.existing)) â†’ true
@(is_error("hello")) â†’ false
```

<a name="function:join"></a>

## join(array, separator)

Joins the given `array` of strings with `separator` to make text.


```objectivec
@(join(array("a", "b", "c"), "|")) â†’ a|b|c
@(join(split("a.b.c", "."), " ")) â†’ a b c
```

<a name="function:json"></a>

## json(value)

Returns the JSON representation of `value`.


```objectivec
@(json("string")) â†’ "string"
@(json(10)) â†’ 10
@(json(null)) â†’ null
@(json(contact.uuid)) â†’ "5d76d86b-3bb9-4d5a-b822-c9d86f5d8e4f"
```

<a name="function:left"></a>

## left(text, count)

Returns the `count` left-most characters in `text`


```objectivec
@(left("hello", 2)) â†’ he
@(left("hello", 7)) â†’ hello
@(left("ğŸ˜€ğŸ˜ƒğŸ˜„ğŸ˜", 2)) â†’ ğŸ˜€ğŸ˜ƒ
@(left("hello", -1)) â†’ ERROR
```

<a name="function:lower"></a>

## lower(text)

Converts `text` to lowercase.


```objectivec
@(lower("HellO")) â†’ hello
@(lower("hello")) â†’ hello
@(lower("123")) â†’ 123
@(lower("ğŸ˜€")) â†’ ğŸ˜€
```

<a name="function:max"></a>

## max(values...)

Returns the maximum value in `values`.


```objectivec
@(max(1, 2)) â†’ 2
@(max(1, -1, 10)) â†’ 10
@(max(1, 10, "foo")) â†’ ERROR
```

<a name="function:mean"></a>

## mean(values)

Returns the arithmetic mean of the numbers in `values`.


```objectivec
@(mean(1, 2)) â†’ 1.5
@(mean(1, 2, 6)) â†’ 3
@(mean(1, "foo")) â†’ ERROR
```

<a name="function:min"></a>

## min(values)

Returns the minimum value in `values`.


```objectivec
@(min(1, 2)) â†’ 1
@(min(2, 2, -10)) â†’ -10
@(min(1, 2, "foo")) â†’ ERROR
```

<a name="function:mod"></a>

## mod(dividend, divisor)

Returns the remainder of the division of `dividend` by `divisor`.


```objectivec
@(mod(5, 2)) â†’ 1
@(mod(4, 2)) â†’ 0
@(mod(5, "foo")) â†’ ERROR
```

<a name="function:now"></a>

## now()

Returns the current date and time in the current timezone.


```objectivec
@(now()) â†’ 2018-04-11T13:24:30.123456-05:00
```

<a name="function:number"></a>

## number(value)

Tries to convert `value` to a number.

An error is returned if the value can't be converted.


```objectivec
@(number(10)) â†’ 10
@(number("123.45000")) â†’ 123.45
@(number("what?")) â†’ ERROR
```

<a name="function:object"></a>

## object(pairs...)

Takes property name value pairs and returns them as a new object.


```objectivec
@(object()) â†’ {}
@(object("a", 123, "b", "hello")) â†’ {a: 123, b: hello}
@(object("a")) â†’ ERROR
```

<a name="function:or"></a>

## or(values...)

Returns whether if any of the given `values` are truthy.


```objectivec
@(or(true)) â†’ true
@(or(true, false, true)) â†’ true
```

<a name="function:parse_datetime"></a>

## parse_datetime(text, format [,timezone])

Parses `text` into a date using the given `format`.

The format string can consist of the following characters. The characters
' ', ':', ',', 'T', '-' and '_' are ignored. Any other character is an error.

* `YY`        - last two digits of year 0-99
* `YYYY`      - four digits of year 0000-9999
* `M`         - month 1-12
* `MM`        - month 01-12
* `D`         - day of month, 1-31
* `DD`        - day of month, zero padded 0-31
* `h`         - hour of the day 1-12
* `hh`        - hour of the day 01-12
* `tt`        - twenty four hour of the day 01-23
* `m`         - minute 0-59
* `mm`        - minute 00-59
* `s`         - second 0-59
* `ss`        - second 00-59
* `fff`       - milliseconds
* `ffffff`    - microseconds
* `fffffffff` - nanoseconds
* `aa`        - am or pm
* `AA`        - AM or PM
* `Z`         - hour and minute offset from UTC, or Z for UTC
* `ZZZ`       - hour and minute offset from UTC

Timezone should be a location name as specified in the IANA Time Zone database, such
as "America/Guayaquil" or "America/Los_Angeles". If not specified, the current timezone
will be used. An error will be returned if the timezone is not recognized.

Note that fractional seconds will be parsed even without an explicit format identifier.
You should only specify fractional seconds when you want to assert the number of places
in the input format.

parse_datetime will return an error if it is unable to convert the text to a datetime.


```objectivec
@(parse_datetime("1979-07-18", "YYYY-MM-DD")) â†’ 1979-07-18T00:00:00.000000-05:00
@(parse_datetime("2010 5 10", "YYYY M DD")) â†’ 2010-05-10T00:00:00.000000-05:00
@(parse_datetime("2010 5 10 12:50", "YYYY M DD tt:mm", "America/Los_Angeles")) â†’ 2010-05-10T12:50:00.000000-07:00
@(parse_datetime("NOT DATE", "YYYY-MM-DD")) â†’ ERROR
```

<a name="function:parse_json"></a>

## parse_json(text)

Tries to parse `text` as JSON.

If the given `text` is not valid JSON, then an error is returned


```objectivec
@(parse_json("{\"foo\": \"bar\"}").foo) â†’ bar
@(parse_json("[1,2,3,4]")[2]) â†’ 3
@(parse_json("invalid json")) â†’ ERROR
```

<a name="function:parse_time"></a>

## parse_time(text, format)

Parses `text` into a time using the given `format`.

The format string can consist of the following characters. The characters
' ', ':', ',', 'T', '-' and '_' are ignored. Any other character is an error.

* `h`         - hour of the day 1-12
* `hh`        - hour of the day 01-12
* `tt`        - twenty four hour of the day 01-23
* `m`         - minute 0-59
* `mm`        - minute 00-59
* `s`         - second 0-59
* `ss`        - second 00-59
* `fff`       - milliseconds
* `ffffff`    - microseconds
* `fffffffff` - nanoseconds
* `aa`        - am or pm
* `AA`        - AM or PM

Note that fractional seconds will be parsed even without an explicit format identifier.
You should only specify fractional seconds when you want to assert the number of places
in the input format.

parse_time will return an error if it is unable to convert the text to a time.


```objectivec
@(parse_time("15:28", "tt:mm")) â†’ 15:28:00.000000
@(parse_time("2:40 pm", "h:mm aa")) â†’ 14:40:00.000000
@(parse_time("NOT TIME", "tt:mm")) â†’ ERROR
```

<a name="function:percent"></a>

## percent(num)

Formats `num` as a percentage.


```objectivec
@(percent(0.54234)) â†’ 54%
@(percent(1.2)) â†’ 120%
@(percent("foo")) â†’ ERROR
```

<a name="function:rand"></a>

## rand()

Returns a single random number between [0.0-1.0).


```objectivec
@(rand()) â†’ 0.3849275689214193274523267973563633859157562255859375
@(rand()) â†’ 0.607552015674623913099594574305228888988494873046875
```

<a name="function:rand_between"></a>

## rand_between()

A single random integer in the given inclusive range.


```objectivec
@(rand_between(1, 10)) â†’ 5
@(rand_between(1, 10)) â†’ 10
```

<a name="function:read_chars"></a>

## read_chars(text)

Converts `text` into something that can be read by IVR systems.

ReadChars will split the numbers such as they are easier to understand. This includes
splitting in 3s or 4s if appropriate.


```objectivec
@(read_chars("1234")) â†’ 1 2 3 4
@(read_chars("abc")) â†’ a b c
@(read_chars("abcdef")) â†’ a b c , d e f
```

<a name="function:regex_match"></a>

## regex_match(text, pattern [,group])

Returns the first match of the regular expression `pattern` in `text`.

An optional third parameter `group` determines which matching group will be returned.


```objectivec
@(regex_match("sda34dfddg67", "\d+")) â†’ 34
@(regex_match("Bob Smith", "(\w+) (\w+)", 1)) â†’ Bob
@(regex_match("Bob Smith", "(\w+) (\w+)", 2)) â†’ Smith
@(regex_match("Bob Smith", "(\w+) (\w+)", 5)) â†’ ERROR
@(regex_match("abc", "[\.")) â†’ ERROR
```

<a name="function:remove_first_word"></a>

## remove_first_word(text)

Removes the first word of `text`.


```objectivec
@(remove_first_word("foo bar")) â†’ bar
@(remove_first_word("Hi there. I'm a flow!")) â†’ there. I'm a flow!
```

<a name="function:repeat"></a>

## repeat(text, count)

Returns `text` repeated `count` number of times.


```objectivec
@(repeat("*", 8)) â†’ ********
@(repeat("*", "foo")) â†’ ERROR
```

<a name="function:replace"></a>

## replace(text, needle, replacement)

Replaces all occurrences of `needle` with `replacement` in `text`.


```objectivec
@(replace("foo bar", "foo", "zap")) â†’ zap bar
@(replace("foo bar", "baz", "zap")) â†’ foo bar
```

<a name="function:replace_time"></a>

## replace_time(date)

Returns the a new date time with the time part replaced by the `time`.


```objectivec
@(replace_time(now(), "10:30")) â†’ 2018-04-11T10:30:00.000000-05:00
@(replace_time("2017-01-15", "10:30")) â†’ 2017-01-15T10:30:00.000000-05:00
@(replace_time("foo", "10:30")) â†’ ERROR
```

<a name="function:right"></a>

## right(text, count)

Returns the `count` right-most characters in `text`


```objectivec
@(right("hello", 2)) â†’ lo
@(right("hello", 7)) â†’ hello
@(right("ğŸ˜€ğŸ˜ƒğŸ˜„ğŸ˜", 2)) â†’ ğŸ˜„ğŸ˜
@(right("hello", -1)) â†’ ERROR
```

<a name="function:round"></a>

## round(num [,places])

Rounds `num` to the nearest value.

You can optionally pass in the number of decimal places to round to as `places`. If `places` < 0,
it will round the integer part to the nearest 10^(-places).


```objectivec
@(round(12)) â†’ 12
@(round(12.141)) â†’ 12
@(round(12.6)) â†’ 13
@(round(12.141, 2)) â†’ 12.14
@(round(12.146, 2)) â†’ 12.15
@(round(12.146, -1)) â†’ 10
@(round("notnum", 2)) â†’ ERROR
```

<a name="function:round_down"></a>

## round_down(num [,places])

Rounds `num` down to the nearest integer value.

You can optionally pass in the number of decimal places to round to as `places`.


```objectivec
@(round_down(12)) â†’ 12
@(round_down(12.141)) â†’ 12
@(round_down(12.6)) â†’ 12
@(round_down(12.141, 2)) â†’ 12.14
@(round_down(12.146, 2)) â†’ 12.14
@(round_down("foo")) â†’ ERROR
```

<a name="function:round_up"></a>

## round_up(num [,places])

Rounds `num` up to the nearest integer value.

You can optionally pass in the number of decimal places to round to as `places`.


```objectivec
@(round_up(12)) â†’ 12
@(round_up(12.141)) â†’ 13
@(round_up(12.6)) â†’ 13
@(round_up(12.141, 2)) â†’ 12.15
@(round_up(12.146, 2)) â†’ 12.15
@(round_up("foo")) â†’ ERROR
```

<a name="function:split"></a>

## split(text, delimiters)

Splits `text` based on the given characters in `delimiters`.

Empty values are removed from the returned list.


```objectivec
@(split("a b c", " ")) â†’ [a, b, c]
@(split("a", " ")) â†’ [a]
@(split("abc..d", ".")) â†’ [abc, d]
@(split("a.b.c.", ".")) â†’ [a, b, c]
@(split("a|b,c  d", " .|,")) â†’ [a, b, c, d]
```

<a name="function:text"></a>

## text(value)

Tries to convert `value` to text.

An error is returned if the value can't be converted.


```objectivec
@(text(3 = 3)) â†’ true
@(json(text(123.45))) â†’ "123.45"
@(text(1 / 0)) â†’ ERROR
```

<a name="function:text_compare"></a>

## text_compare(text1, text2)

Returns the dictionary order of `text1` and `text2`.

The return value will be -1 if `text1` comes before `text2`, 0 if they are equal
and 1 if `text1` comes after `text2`.


```objectivec
@(text_compare("abc", "abc")) â†’ 0
@(text_compare("abc", "def")) â†’ -1
@(text_compare("zzz", "aaa")) â†’ 1
```

<a name="function:text_length"></a>

## text_length(value)

Returns the length (number of characters) of `value` when converted to text.


```objectivec
@(text_length("abc")) â†’ 3
@(text_length(array(2, 3))) â†’ 6
```

<a name="function:time"></a>

## time(value)

Tries to convert `value` to a time.

If it is text then it will be parsed into a time using the default time format.
An error is returned if the value can't be converted.


```objectivec
@(time("10:30")) â†’ 10:30:00.000000
@(time("10:30:45 PM")) â†’ 22:30:45.000000
@(time(datetime("1979-07-18T10:30:45.123456Z"))) â†’ 10:30:45.123456
@(time("what?")) â†’ ERROR
```

<a name="function:time_from_parts"></a>

## time_from_parts(hour, minute, second)

Creates a time from `hour`, `minute` and `second`


```objectivec
@(time_from_parts(14, 40, 15)) â†’ 14:40:15.000000
@(time_from_parts(8, 10, 0)) â†’ 08:10:00.000000
@(time_from_parts(25, 0, 0)) â†’ ERROR
```

<a name="function:title"></a>

## title(text)

Capitalizes each word in `text`.


```objectivec
@(title("foo")) â†’ Foo
@(title("ryan lewis")) â†’ Ryan Lewis
@(title("RYAN LEWIS")) â†’ Ryan Lewis
@(title(123)) â†’ 123
```

<a name="function:today"></a>

## today()

Returns the current date in the environment timezone.


```objectivec
@(today()) â†’ 2018-04-11
```

<a name="function:tz"></a>

## tz(date)

Returns the name of the timezone of `date`.

If no timezone information is present in the date, then the current timezone will be returned.


```objectivec
@(tz("2017-01-15T02:15:18.123456Z")) â†’ UTC
@(tz("2017-01-15 02:15:18PM")) â†’ America/Guayaquil
@(tz("2017-01-15")) â†’ America/Guayaquil
@(tz("foo")) â†’ ERROR
```

<a name="function:tz_offset"></a>

## tz_offset(date)

Returns the offset of the timezone of `date`.

The offset is returned in the format `[+/-]HH:MM`. If no timezone information is present in the date,
then the current timezone offset will be returned.


```objectivec
@(tz_offset("2017-01-15T02:15:18.123456Z")) â†’ +0000
@(tz_offset("2017-01-15 02:15:18PM")) â†’ -0500
@(tz_offset("2017-01-15")) â†’ -0500
@(tz_offset("foo")) â†’ ERROR
```

<a name="function:upper"></a>

## upper(text)

Converts `text` to lowercase.


```objectivec
@(upper("Asdf")) â†’ ASDF
@(upper(123)) â†’ 123
```

<a name="function:url_encode"></a>

## url_encode(text)

Encodes `text` for use as a URL parameter.


```objectivec
@(url_encode("two & words")) â†’ two%20%26%20words
@(url_encode(10)) â†’ 10
```

<a name="function:urn_parts"></a>

## urn_parts(urn)

Parses a URN into its different parts


```objectivec
@(urn_parts("tel:+593979012345")) â†’ {display: , path: +593979012345, scheme: tel}
@(urn_parts("twitterid:3263621177#bobby")) â†’ {display: bobby, path: 3263621177, scheme: twitterid}
```

<a name="function:weekday"></a>

## weekday(date)

Returns the day of the week for `date`.

The week is considered to start on Sunday so a Sunday returns 0, a Monday returns 1 etc.


```objectivec
@(weekday("2017-01-15")) â†’ 0
@(weekday("foo")) â†’ ERROR
```

<a name="function:word"></a>

## word(text, index [,delimiters])

Returns the word at `index` in `text`.

Indexes start at zero. There is an optional final parameter `delimiters` which
is string of characters used to split the text into words.


```objectivec
@(word("bee cat dog", 0)) â†’ bee
@(word("bee.cat,dog", 0)) â†’ bee
@(word("bee.cat,dog", 1)) â†’ cat
@(word("bee.cat,dog", 2)) â†’ dog
@(word("bee.cat,dog", -1)) â†’ dog
@(word("bee.cat,dog", -2)) â†’ cat
@(word("bee.*cat,dog", 1, ".*=|")) â†’ cat,dog
@(word("O'Grady O'Flaggerty", 1, " ")) â†’ O'Flaggerty
```

<a name="function:word_count"></a>

## word_count(text [,delimiters])

Returns the number of words in `text`.

There is an optional final parameter `delimiters` which is string of characters used
to split the text into words.


```objectivec
@(word_count("foo bar")) â†’ 2
@(word_count(10)) â†’ 1
@(word_count("")) â†’ 0
@(word_count("ğŸ˜€ğŸ˜ƒğŸ˜„ğŸ˜")) â†’ 4
@(word_count("bee.*cat,dog", ".*=|")) â†’ 2
@(word_count("O'Grady O'Flaggerty", " ")) â†’ 2
```

<a name="function:word_slice"></a>

## word_slice(text, start, end [,delimiters])

Extracts a sub-sequence of words from `text`.

The returned words are those from `start` up to but not-including `end`. Indexes start at zero and a negative
end value means that all words after the start should be returned. There is an optional final parameter `delimiters`
which is string of characters used to split the text into words.


```objectivec
@(word_slice("bee cat dog", 0, 1)) â†’ bee
@(word_slice("bee cat dog", 0, 2)) â†’ bee cat
@(word_slice("bee cat dog", 1, -1)) â†’ cat dog
@(word_slice("bee cat dog", 1)) â†’ cat dog
@(word_slice("bee cat dog", 2, 3)) â†’ dog
@(word_slice("bee cat dog", 3, 10)) â†’
@(word_slice("bee.*cat,dog", 1, -1, ".*=|,")) â†’ cat dog
@(word_slice("O'Grady O'Flaggerty", 1, 2, " ")) â†’ O'Flaggerty
```


</div>
